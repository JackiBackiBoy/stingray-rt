#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : require

#include "includes/bindless.glsl"
#include "includes/geometry_types.glsl"
#include "includes/ray_payload.glsl"
#include "includes/ray_tracing_math.glsl"

layout (location = 0) rayPayloadEXT RayPayload rayPayload;

layout (set = BINDLESS_DESCRIPTOR_SET, binding = BINDLESS_TLAS_BINDING) uniform accelerationStructureEXT g_TLAS;

layout (push_constant) uniform constants {
    uint frameIndex;
    uint rtAccumulationIndex;
    uint rtImageIndex;
    uint sceneDescBufferIndex;
    uint samplesPerPixel;
    uint totalSamplesPerPixel;
} g_PushConstants;

const uint RAY_BOUNCES = 16;

void main() {
    const vec2 pixelCoord = vec2(gl_LaunchIDEXT.xy);
    uint rngSeed = g_PushConstants.totalSamplesPerPixel;
    rayPayload.rngSeed = InitRandomSeed(InitRandomSeed(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y), g_PushConstants.totalSamplesPerPixel);

    const uint stratumDim = uint(sqrt(float(g_PushConstants.samplesPerPixel)));
    const float stratumSize = 1.0 / float(stratumDim);

    vec3 color = vec3(0.0);
    for (uint sy = 0; sy < 1; sy++) {
        for (uint sx = 0; sx < stratumDim; sx++) {
            const vec2 jitter = vec2(RandomFloat(rngSeed), RandomFloat(rngSeed)) * stratumSize;
            const vec2 stratumCoord = pixelCoord + stratumSize * vec2(sx, sy) + jitter;
            const vec2 inUV = stratumCoord / vec2(gl_LaunchSizeEXT.xy);

            vec2 ndcXY = inUV * 2.0 - 1.0;
            ndcXY.y *= -1.0;
            vec4 rayOrigin = g_PerFrameData[g_PushConstants.frameIndex].invViewProjection * vec4(ndcXY, 0.0, 1.0);
            vec4 rayEnd = g_PerFrameData[g_PushConstants.frameIndex].invViewProjection * vec4(ndcXY, 1.0, 1.0);

            rayOrigin.xyz /= rayOrigin.w;
            rayEnd.xyz /= rayEnd.w;

            vec3 rayDir = normalize(rayEnd.xyz - rayOrigin.xyz);
            vec3 rayColor = vec3(1.0);

            for (uint j = 0; j <= RAY_BOUNCES; j++) {
                if (j == RAY_BOUNCES) {
                    rayColor = vec3(0.0);
                    break;
                }

                uint rayFlags = gl_RayFlagsNoneEXT;
                traceRayEXT(
                    g_TLAS,         // acceleration structure
                    rayFlags,       // rayFlags
                    0xFF,           // cullMask
                    0,              // sbtRecordOffset
                    0,              // sbtRecordStride
                    0,              // missIndex
                    rayOrigin.xyz,  // ray origin
                    0.001,          // ray min range
                    rayDir,         // ray direction
                    10000.0,        // ray max range
                    0               // payload (location = 0)
                );

                rayColor *= rayPayload.color;

                if (rayPayload.distance < 0 || !rayPayload.isScattered) {
                    break;
                }

                rayOrigin.xyz += rayPayload.distance * rayDir;
                rayDir = rayPayload.scatterDir;
            }

            color += rayColor;
        }
    }

    vec3 accumulatedColor = color;
    // NOTE: When accumulation is reset, we set totalSamplesPerPixel to
    // samplesPerPixel from CPU-side, so this condition will only ever
    // be false the first time.
    if (g_PushConstants.totalSamplesPerPixel != g_PushConstants.samplesPerPixel) {
        accumulatedColor += imageLoad(
            g_RWTexturesRGBA8[g_PushConstants.rtAccumulationIndex],
            ivec2(gl_LaunchIDEXT.xy)
        ).rgb;
    }

    color = accumulatedColor / g_PushConstants.totalSamplesPerPixel;

    imageStore(
        g_RWTexturesRGBA8[g_PushConstants.rtAccumulationIndex],
        ivec2(gl_LaunchIDEXT.xy),
        vec4(accumulatedColor, 1.0)
    );

    // Get display color
	color = accumulatedColor / g_PushConstants.totalSamplesPerPixel;
	// Gamma correction
	color = sqrt(color);

    imageStore(
        g_RWTexturesRGBA8[g_PushConstants.rtImageIndex],
        ivec2(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y),
        vec4(color, 1.0)
    );
}